#-------------------------------------------
# D*-entailment rules
#-------------------------------------------

[rdfs2:  (?p rdfs:domain ?c), (?x ?p ?y) -> (?x rdf:type ?c)]
[rdfs3:  (?p rdfs:range ?c), (?x ?p ?y)  -> (?y rdf:type ?c) ]
[rdfs5: (?a rdfs:subPropertyOf ?b), (?b rdfs:subPropertyOf ?c) -> (?a rdfs:subPropertyOf ?c)]
[rdfs7:  (?p rdfs:subPropertyOf ?q), (?a ?p ?b), notEqual(?p,?q) -> (?a ?q ?b)]
[rdfs9:  (?x rdfs:subClassOf ?y), (?a rdf:type ?x) -> (?a rdf:type ?y) ]
[rdfs11:  (?a rdfs:subClassOf ?b), (?b rdfs:subClassOf ?c) -> (?a rdfs:subClassOf ?c)]


#-------------------------------------------
# P-entailment rules
#-------------------------------------------
[rdfp1:
	(?P rdf:type owl:FunctionalProperty), (?A ?P ?B), notLiteral(?B), (?A ?P ?C), notLiteral(?C), notEqual(?B ?C) -> (?B owl:sameAs ?C)]

[rdfp2:
   	(?P rdf:type owl:InverseFunctionalProperty), (?A ?P ?B), (?C ?P ?B), notEqual(?A ?C) -> (?A owl:sameAs ?C)]

[rdfp3:
	(?P rdf:type owl:SymmetricProperty), (?X ?P ?Y) -> (?Y ?P ?X)]

[rdfp4:
	(?P rdf:type owl:TransitiveProperty), (?X ?P ?Y), (?Y ?P ?Z) -> (?X ?P ?Z)]

[rdfp5a:
    (?X ?Q ?Y) -> (?X owl:sameAs ?X)]

[rdfp5b:
    (?X ?P ?Y) -> (?Y owl:sameAs ?Y)]

[rdfp6:
	(?A owl:sameAs ?B) -> (?B owl:sameAs ?A)]

[rdfp7:
	(?A owl:sameAs ?B), (?B owl:sameAs ?C) -> (?A owl:sameAs ?C)]

[rdfp8ax:
	(?P owl:inverseOf ?Q), (?X ?P ?Y) -> (?Y ?Q ?X)]

[rdfp8bx:
	(?P owl:inverseOf ?Q), (?X ?Q ?Y) -> (?Y ?P ?X)]

[rdfp12a:
	(?C owl:equivalentClass ?D) -> (?C rdfs:subClassOf ?D )]

[rdfp12b:
	(?C owl:equivalentClass ?D) -> (?D rdfs:subClassOf ?C )]

[rdfp12c:
	(?C rdfs:subClassOf ?D ), (?D rdfs:subClassOf ?C ) -> (?C owl:equivalentClass ?D)]

[rdfp13a:
	(?C owl:equivalentProperty ?D) -> (?C rdfs:subPropertyOf ?D )]

[rdfp13b:
	(?C owl:equivalentProperty ?D) -> (?D rdfs:subPropertyOf ?C )]

[rdfp13c:
	(?C rdfs:subPropertyOf ?D ), (?D rdfs:subPropertyOf ?C ) -> (?C owl:equivalentProperty ?D)]

[rdfp14a:
	(?R owl:hasValue ?V), (?R owl:onProperty ?P), (?U ?P ?V) -> (?U rdf:type ?R)]

[rdfp14bx:
	(?R owl:hasValue ?V), (?R owl:onProperty ?P), (?U rdf:type ?R) -> (?U ?P ?V)]

[rdfp15:
	(?R owl:someValuesFrom ?D), (?R owl:onProperty ?P), (?X ?P ?A), (?A rdf:type ?D ) -> (?X rdf:type ?R )]

[rdfp16:
	(?R owl:allValuesFrom ?D), (?R owl:onProperty ?P), (?X ?P ?Y), (?X rdf:type ?R ) -> (?Y rdf:type ?D )]